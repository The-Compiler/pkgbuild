# Maintainer: Florian Bruhin (The-Compiler) <archlinux.org@the-compiler.org>
# Contributor: JD Horelick <jdhore1@gmail.com>

pkgname=anope-devel
pkgver=1.9.8
pkgrel=1
pkgdesc="A set of IRC Services designed for flexibility and ease of use"
arch=('i686' 'x86_64')
url="http://anope.org/"
license=('GPL') 
install='anope.install'
depends=('mysql')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/sourceforge/anope/anope-${pkgver}-source.tar.gz"
        'anope.install'
	'anope.service'
	'anope.tmpfiles')
sha1sums=('ec01aab52decd19ea807a233cb37c8888f0f6654'
          '1a08e4808e142eb460b5e3866100f1919e231f4c'
          'dfd7717945d04f308a1c2c3512c5beda4902f276'
          '7d38218fef729433ca32b81514264beea9981cf8')

build() {
  cd "${srcdir}/anope-${pkgver}-source"

  # Since we want ./Config to be non-interactive, we create config.cache here
  cat << EOF > config.cache
    INSTDIR="${srcdir}/install"
    RUNGROUP="anope"
    UMASK=077
    DEBUG="no"
    USE_RUN_CC_PL="no"
    USE_PCH="no"
    EXTRA_INCLUDE_DIRS=""
    EXTRA_LIB_DIRS=""
    EXTRA_CONFIG_ARGS=""
EOF

  ./Config -nointro -quick
  cd build
  make
}

package() {
  cd "${srcdir}/anope-${pkgver}-source/build"
  # Here we install it to a temporary directory so we can install the files in
  # more sensible places.
  make install
  cd "${srcdir}/install"

  ### Create empty directories
  install -o142 -g142 -dm755 \
          "${pkgdir}"{/var/log,/var/lib,/etc,/usr/lib}/anope
  install -o142 -g142 -dm755 "${pkgdir}/var/lib/anope/runtime"
  install -dm755 "${pkgdir}/usr/bin"
  install -o142 -g142 -dm755 "${pkgdir}/usr/lib/anope"/{modules,locale}
  install -dm755 "${pkgdir}/usr/lib/systemd/system"
  install -dm755 "${pkgdir}/etc/tmpfiles.d"

  ### Copy files
  # we don't install anoperc because systemd can do that all
  install -Dm755 bin/{anopesmtp,services} "${pkgdir}/usr/bin"
  # we don't copy example.chk (the crontab script) because it makes no sense
  install -o142 -g142 -Dm644 conf/*.conf "${pkgdir}/etc/anope"
  cp -R data "${pkgdir}/var/lib/anope"
  chown -R 142:142 "${pkgdir}/var/lib/anope"
  install -o142 -g142 -Dm644 lib/modules/* "${pkgdir}/usr/lib/anope/modules"
  cp -R locale/* "${pkgdir}/usr/lib/anope/locale"
  chown -R 142:142 "${pkgdir}/usr/lib/anope/locale"
  install -Dm644 "${srcdir}/anope.service" "${pkgdir}/usr/lib/systemd/system"
  install -Tm644 "${srcdir}/anope.tmpfiles" "${pkgdir}/etc/tmpfiles.d/anope.conf"
}
