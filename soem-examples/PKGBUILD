# Maintainer: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=soem-examples
pkgver=1.3.0
pkgrel=2
pkgdesc="Simple Open EtherCAT Master (SOEM) - an EtherCAT (Ethernet for Control Automation Technology) master library written in C (example binaries)"
arch=('i686' 'x86_64')
url="http://soem.berlios.de/"
depends=('glibc')
license=('gpl2')
source=("http://download.berlios.de/soem/SOEM${pkgver}.tar.bz2"
        'osal.patch')
sha1sums=('0e58b8cf1876dc2e506e43a7c3327d76acecfe70'
          'ca261467d1034e0dd69a77e9c77d3e2170171cbb')

prepare() {
  cd "${srcdir}/SOEM${pkgver}"
  patch -p0 -i "${srcdir}/osal.patch"
}

build() {
  cd "${srcdir}/SOEM${pkgver}"
  export ARCH=linux
  export BSP=linux
  export CROSS_GCC=linux
  export GCC_PATH=/usr/bin
  export PRJ_ROOT="${srcdir}/SOEM${pkgver}"

  make -C osal -j1 # FIXME remove this once it's fixed upstream
  make -j1
}

package() {
  cd "${srcdir}/SOEM${pkgver}/test/linux"
  for bin in ebox eepromtool firm_update red_test simple_test slaveinfo; do
    install -Dm 755 "${bin}/${bin}" "${pkgdir}/usr/bin/${bin}"
  done
}

# vim:set ts=2 sw=2 et:
